#!/usr/bin/perl

use strict;
use warnings;

use Template;
use File::Find::Rule;
use File::Path qw(make_path remove_tree);
use File::Basename qw (fileparse dirname);
use File::Copy::Recursive qw(fcopy dirmove);
use File::Temp qw (tempdir);

my $out_dir = shift || "out/";

#find all files

#rule to match git directorys
my $git = File::Find::Rule->directory
                          ->name(".git")
                          ->prune
                          ->discard;

#matches all files
my $file_rule = File::Find::Rule->file();

#combine both
my @files = File::Find::Rule->or( $git, $file_rule )
                            ->in('.');

#create a tempdir
my $tempdir = tempdir( CLEANUP => 1 );

#initialize template toolkit

my $template = Template->new;

foreach my $file (@files) {
	next if $file =~ /^$out_dir/;
	next if $file =~ /$0$/;
	if ($file =~ /\.tt2$/) {
		my $output;
		$template->process($file, undef, \$output)
			|| die "Could not process file \"$file\": $!";

		my ($name,$path,$suffix) = fileparse($file,qw (.tt2));
        make_path("$tempdir/$path") unless -d "$tempdir/$path";
		open (my $fh, '>', "$tempdir/$path/$name")
			or die "Could not write to $file: $!";
		print $fh $output;
		close($fh);
	} else {
		fcopy ($file, "$tempdir/$file") or die "Could not copy $file to $tempdir/$file: $!";
	}
}

$out_dir =~ s/\/$//;
if (-d $out_dir) {
    dirmove ($out_dir, $out_dir . ".bak")
        or die "Could not move $out_dir to $out_dir.bak: $!";
}
if (! dirmove ($tempdir, "$out_dir")) {
	warn "Could not move $tempdir to $out_dir: $!";
	warn "Rollback";
	remove_tree($out_dir);
	dirmove ($out_dir . ".bak", $out_dir);
} else {
	remove_tree($out_dir . ".bak");
}
